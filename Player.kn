{
  プレイヤー
}

const width : float :: 100.0
const height : float :: 160.0
const renderOffsetX : float :: -(@width / 2.0)
const renderOffsetY : float :: -@height

class Player(Actor@Actor)
  const maxHp : int :: 3
  const invincibleFrames : int :: 2 * 60
  var hp : int
  var invincibleCount : int
  var animation : Animation@Animation

  func IsInvincible() : bool
    return this.invincibleCount > 0
  end func
end class

class Test()
  func Test(n : int)
  end func
end class

{ 範囲補正 }
func Correct(n : &float, min : float, max : float) : bool
  if (n < min)
    do n :: min
    return true
  elif (n > max)
    do n :: max - 1.0
    return true
  end if
  return false
end func

{ 初期化 }
func Create(x : float, y : float) : @Player
  const left : float :: -(@width * 0.8 / 2.0)
  const top : float :: -(@height * 0.9)
  const right : float :: @width * 0.8 / 2.0
  const bottom : float :: 0.0
  var player : @Player :: #@Player
  do player.Initialize(Main@MakePosition(x, y), Main@MakeRect(left, top, right, bottom))
  do player.hp :: @Player#maxHp
  do player.invincibleCount :: 0
  do player.animation :: (#Animation@Animation).Animation("player.kntex", Main@MakeRect(200.0, 100.0, 300.0, 300.0))
  return player
end func

{ 操作 }
func Action(resource : Main@Resource, actor : @Player, shotList : Shot@ShotList)
  { 左右移動 }
  const runVelocity : float :: 6.0
  const maxRunVelocity : float :: 16.0
  var velocity : Main@Vector2 :: ##actor.velocity
  if (Input@Pad(0, Input@EBtn#Left) > 0)
    do actor.direction :: Main@MakeVector2(-1.0, 0.0)
    do velocity.x :- runVelocity
    if (velocity.x < -maxRunVelocity)
      do velocity.x :: -maxRunVelocity
    end if
  elif (Input@Pad(0, Input@EBtn#Right) > 0)
    do actor.direction :: Main@MakeVector2(1.0, 0.0)
    do velocity.x :+ runVelocity
    if (velocity.x > maxRunVelocity)
      do velocity.x :: maxRunVelocity
    end if
  else
    if (velocity.x > 0.0)
      do velocity.x :- runVelocity
      if (velocity.x < 0.0)
        do velocity.x :: 0.0
      end if
    elif (velocity.x < 0.0)
      do velocity.x :+ runVelocity
      if (velocity.x < 0.0)
        do velocity.x :: 0.0
      end if
    end if
  end if

  { ジャンプ }
  if (Input@Pad(0, Input@EBtn#B) = 1)
    var jumpPower : float :: -15.0 * Lib@Sqrt(2.0 * Main@gravity)
    do velocity.y :: jumpPower
  end if

  { ショット }
  if (Input@Pad(0, Input@EBtn#C) = 1)
    var pos : Main@Position :: ##actor.position
    do pos.y :- (actor.rect.bottom - actor.rect.top) * 0.8
    do shotList.Add(pos, (##actor.direction).MulScalar(32.0))
  end if
  { 重力制御 }
  do velocity.y  :+ Main@gravity
  const topLimit : float :: -@height
  const bottomLimit : float :: Map@height $ float * Map@blockH + @height - 10.0
  if (@Correct(&actor.position.y, topLimit, bottomLimit))
    do actor.velocity.y  :: 0.0
  end if

  do actor.velocity :: velocity

  var result : Map@CollisionResult :: Map@Collision(actor)

  const leftLimit : float :: @width / 2.0
  const rightLimit : float :: Map@width $ float * Map@blockW - @width / 2.0
  do @Correct(&actor.position.x, leftLimit, rightLimit)

  if (actor.invincibleCount > 0)
    do actor.invincibleCount :- 1
  end if
end func

{ 描画 }
func Draw(actor : @Player, camera : Main@Position)
  if (actor.invincibleCount % 2 = 0)
    var x : float :: actor.position.x + @renderOffsetX - camera.x
    var y : float :: actor.position.y + @renderOffsetY - camera.y - 40.0
    do actor.animation.Draw(x, y, Main@MakeColorRGBA(1.0, 1.0, 1.0, 1.0), actor.direction.x < 0.0 ?(Animation@Direction#Left, Animation@Direction#Right))
  end if
end func
